
import com.buildconfig.internal.model.ModuleArtifactCoordinates

/**
 * Module identifier, used across the project to reference this module.
 * This should match the directory name this file is in.
 * */
ext.module_name = "catch-android-sdk"

/**
 * Deploying Coordinates for local module.
 *
 * You should modify (ona a per-module basis):
 *   - PUBLISH_GROUP_ID
 *   - PUBLISH_ARTIFACT_ID
 *   - PUBLISH_ARTIFACT_DESC
 *   - PUBLISH_ARTIFACT_WEBSITE
 *   - PUBLISH_VERSION
 * Accordingly to your library publication artifact(s)
 * */

project.ext.set("module_${ext.module_name}", new ModuleArtifactCoordinates(
        // PUBLISH_GROUP_ID
        "com.getcatch",
        
        // PUBLISH_ARTIFACT_ID
        "catch-android-sdk",
        
        // PUBLISH_VERSION
        "0.0.0",
        
        // PUBLISH_ARTIFACT_DESC
        "Android SDK for Catch",
        
        // PUBLISH_ARTIFACT_WEBSITE
        "https://github.com/getcatch/catch-android-sdk"
))

// Android library script
apply from: rootProject.file("scripts/commons/library_config.gradle")

// MavenCentral publication script
apply from: rootProject.file("scripts/publishing/publish_mavencentral.gradle")


/**
 * Library versioning
 * */
android {
    defaultConfig {
        versionName "1.0.0-library-module"
        versionCode 3
    }
    buildFeatures {
        viewBinding true
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.3.0"
    }
}


/**
 * Library dependencies
 * */
dependencies {
    // Kotlin dependencies
    compileOnly "androidx.annotation:annotation:1.2.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation "androidx.core:core-ktx:1.8.0"
    implementation "androidx.appcompat:appcompat:1.5.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    
    // COMPOSE DEPENDENCIES
    // Integration with activities
    api 'androidx.activity:activity-compose:1.5.1'
    // Compose Material Design
    api 'androidx.compose.material:material:1.2.1'
    // Animations
    api 'androidx.compose.animation:animation:1.2.1'
    // Tooling support (Previews, etc.)
    api 'androidx.compose.ui:ui-tooling:1.2.1'
    // Integration with ViewModels
    api 'androidx.lifecycle:lifecycle-viewmodel-compose:2.5.1'
    // UI Tests
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4:1.2.1'
    
    // Testing dependencies
    testImplementation "junit:junit:4.13.2"
    testImplementation "androidx.test:core:1.4.0"
    testImplementation "org.mockito:mockito-core:4.4.0"
    testImplementation "com.google.truth:truth:1.1.3"
    testImplementation "org.robolectric:robolectric:4.7.3"
}


tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        // Explicit API Mode
        freeCompilerArgs += [
                //'-progressive',
                '-Xexplicit-api=strict',
        ]
    }
}
